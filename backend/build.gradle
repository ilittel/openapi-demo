plugins {
    id 'application'
    id 'org.openapi.generator' version '5.1.0'
}

version '1.0-SNAPSHOT'
group 'org.openapidemo'
description 'OpenAPI demo back-end'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    //runtimeOnly project(':frontend')
    implementation 'io.dropwizard:dropwizard-core:2.0.20'
    implementation 'io.swagger:swagger-annotations:1.5.3'
}

sourceSets {
    main {
        java {
            srcDir 'openapi/src/gen/java'
        }
    }
}

application {
    // Define the main class for the application.
    mainClass = 'org.openapidemo.BackendApp'
}

openApiGenerate {
    // For all generators and options, see https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators.md
    generatorName = 'jaxrs-spec'
    // NOTE: Double quotes below are important!
    inputSpec = "${rootDir}/api/ilittel-Products-1.0.0-unresolved.yaml"
    outputDir = "${projectDir}/openapi"
    apiPackage = 'org.openapidemo.api'
    invokerPackage = 'org.openapidemo.invoker'
    modelPackage = 'org.openapidemo.model'
    configOptions = [
        interfaceOnly: 'true',
        dateLibrary: 'java8'
    ]
    globalProperties = [
        // Notes on the generation properties below:
        // 1) An empty value means 'generate all'.
        // 2) If you specify one, the others will NOT be generated.
        apis: '',
        models: '',
        //supportingFiles: ''
    ]
}

task cleanGeneratedCode(type: Delete) {
    delete openApiGenerate.outputDir
}

tasks.compileJava.dependsOn(tasks.openApiGenerate)
tasks.openApiGenerate.dependsOn(tasks.cleanGeneratedCode)
tasks.clean.dependsOn(tasks.cleanGeneratedCode)
